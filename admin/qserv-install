#!/usr/bin/perl -w

use strict;
use Getopt::Long;
use Cwd;
use FindBin;
use Sys::Hostname;
use File::Path;

Getopt::Long::config('bundling_override');
my %opts = ();
GetOptions( \%opts, 
	"debug",
	"help|h",
	"install-dir|d=s",
	"log-dir|l=s",
	"build-dir=s",
	"clean-all",
	"extras",
	"lua",
	"master=s",
	"mysql",
	"init-mysql-db",
	"mysql-port=s",
	"mysql-data-dir|m=s",
	"mysql-pass=s", # TODO: avoid passing passwords in command line parameters !
	"mysql-proxy",
	"python=s",
	"xrootd",
	"qserv",
	"user",
	"test",
	"geometry-dir=s"
);

my $script_path = "$FindBin::Bin/../";
my $pos = rindex($script_path, "/admin");
my $src_dir=substr($script_path,0,$pos);

my $templates_dir="$src_dir/admin/custom";

usage(1) if ($Getopt::Long::error); 
usage(0) if ($opts{'help'});

my $debug = $opts{'debug'} || 0;

my $install_dir = $opts{'install-dir'} || cwd();
my $log_dir = $opts{'log-dir'} || "$install_dir/var/log";
my $mysql_data_dir = $opts{'mysql-data-dir'} || "$install_dir/var/lib/mysql";
my $mysql_port = $opts{'mysql-port'} || "3306";
my $db_pass = $opts{'mysql-pass'};
my $build_dir   = $opts{'build-dir'} || "$install_dir/build";
my $prod_user   = $opts{'user'} || $ENV{'USER'};
my $geometry_dir = $opts{'geometry-dir'};

$install_dir = remove_trailing_slash($install_dir);
$log_dir = remove_trailing_slash($log_dir);
$mysql_data_dir = remove_trailing_slash($mysql_data_dir);
$build_dir = remove_trailing_slash($build_dir);

my $fqdn = system("hostname -f");

if ($geometry_dir) {
    $geometry_dir = remove_trailing_slash($geometry_dir);
}

print "hostname :  $fqdn\n" if( $debug );

my $build_opt = "all";
$build_opt = "lua" if( $opts{'lua'} );
$build_opt = "mysql" if( $opts{'mysql'} );
$build_opt = "init-mysql-db" if( $opts{'init-mysql-db'} );
$build_opt = "xrootd" if( $opts{'xrootd'} );
$build_opt = "qserv" if( $opts{'qserv'} );
$build_opt = "mysql-proxy" if( $opts{'mysql-proxy'} );
$build_opt = "extras" if( $opts{'extras'} );

#set python path for use
my $python_path = $opts{'python'} || '/usr/bin/python';

chdir( $install_dir );

my @buildfiles;
list_build_dir();

if( $opts{'clean-all'} ) {
	clean_all();
	exit(0);
}

# qserv templates configuration files will be used by other installs
check_and_deploy_qserv();

#setup the python env.
check_and_setup_python();

$ENV{"PATH"} = "$install_dir/bin:".$ENV{"PATH"};

#step one, setup xrootd software
check_and_install_xrootd() if( $build_opt eq "all" or $build_opt eq "xrootd" );

#setup mysql
check_and_install_mysql() if( $build_opt eq "all" or $build_opt eq "mysql" );

init_mysql_db() if( $build_opt eq "all" or $build_opt eq "mysql" or $build_opt eq "init-mysql-db");

#setup and install python extras.
check_and_setup_extras() if( $build_opt eq "all" or $build_opt eq "extras" );

#install lua
check_and_install_lua() if( $build_opt eq "all" or $build_opt eq "lua" );

#install mysql proxy
check_and_install_myproxy() if( $build_opt eq "all" or $build_opt eq "mysql-proxy" );

#setup the qserv code
check_and_install_qserv() if( $build_opt eq "all" or $build_opt eq "qserv" );

print "PROCESS SUCCESSFULLY COMPLETED\n";

#############################################################

sub list_build_dir {
	#get list of files in build dir
	opendir BUILDIR, "$build_dir" or die print "Can't open $build_dir : $!\n";
	@buildfiles = readdir BUILDIR;
	closedir BUILDIR;
}

#check on the xrootd source tar, untar, compile, configure for use.
sub check_and_install_xrootd {
	
	#check if it is already installed
	if( -e "$install_dir/xrootd" ) {
		print "WARNING: Xrootd already installed...\n";
		return;
	}
	
	#untar xrootd
	if( -e "$build_dir/xrootd.tar.gz" ) {
		if( $opts{'test'} ) {
			print "I would untar xrootd.tar.gz here.\n";
		} else {
			print "Untaring xrootd...\n";
			run_command("tar zxf $build_dir/xrootd.tar.gz");
		}
	} else {
		print "Sorry - I can't find the xrootd.tar.gz in the build dir.\n";
		exit(0);
	}
	
	chdir "$install_dir/xrootd";
	
	#setup and configure for rhel 6
	my $configure = "./configure.classic --prefix=$install_dir --disable-afs ".
		"--disable-gsi --enable-trace --disable-krb4 --disable-krb5 ".
		"--disable-ssl --disable-secssl --build=debug";
	
	print "Configuring xrootd for install.\n";
	run_command("$configure");
	
	print "Building and installing xrootd.\n";
	run_command("make");
	run_command("make install");

	chdir "$install_dir";
	mkdir "xrootd-run";
	mkdir "xrootd-run/result";
	mkdir "xrootd-run/q";
	
}

#check on qserv source tar, compile, install, and configure for use.
sub check_and_install_qserv {
	my $pass_opt = '';

	if ( length($db_pass) > 0) {
		$pass_opt = "-p'$db_pass'"
	}

	print "Installing Qserv from source.\n";
	check_and_deploy_qserv();
	
	if( -e "$install_dir/qserv/master/dist" ) {	
		#looks like qserv is already installed so, move on
		print "WARNING: Not installing Qserv because it looks like it is already installed in $install_dir/qserv\n";
		print "WARNING: Please remove $install_dir/qserv/master/dist in order to force installation.\n";
		return;
	}
	
	chdir "$install_dir/qserv/common";
	$ENV{'XRD_DIR'} = "$install_dir/xrootd";
	$ENV{'XRD_PLATFORM'} = "x86_64_linux_26_dbg";
	$ENV{'PROTOC'} = "$install_dir/bin/protoc";
	$ENV{'PROTOC_INC'} = "$install_dir/include";
	$ENV{'PROTOC_LIB'} = "$install_dir/lib";
	$ENV{'MYSQL_ROOT'} = "$install_dir";
	$ENV{'SEARCH_ROOTS'} = "$install_dir";
	run_command("scons");
	run_command("scons master");
	
	#install the geometry
	if (not -e "$src_dir/master/python/lsst/qserv/master/geometry.py") {
		if (not $geometry_dir) {
			print "Downloading geometry file in geom master branch, Use --geometry-dir to specify a geometry source directory, get it with `git clone git://dev.lsstcorp.org/LSST/DMS/geom`"; 
			chdir "$install_dir/qserv/master/python/lsst/qserv/master/" or die print "$!\n";
			run_command("wget http://dev.lsstcorp.org/cgit/LSST/DMS/geom.git/plain/python/lsst/geom/geometry.py");
		}
		else {
			print "Using --geometry-dir=$geometry_dir as geometry source directory";
			run_command("cp -r $geometry_dir/python/lsst/geom/geometry.py $install_dir/qserv/master/python/lsst/qserv/master/geometry.py");
		}
	} 
	chdir "$install_dir/qserv/master";
	run_command("scons");
	run_command("scons install");
	
	chdir "$install_dir/qserv/worker";
	run_command("scons");
	
	#start mysql server and install init database
	chdir "$install_dir";
	print "Starting mysql server.\n";
	system("$install_dir/bin/mysqld_safe --defaults-file=$install_dir/etc/my.cnf &");
	sleep 5;
	print "Setting up init databases.\n";
	run_command("./bin/mysql -S var/lib/mysql/mysql.sock -u root $pass_opt -e \"CREATE DATABASE IF NOT EXISTS qservResult;\"");
	run_command("./bin/mysql -S var/lib/mysql/mysql.sock -u root $pass_opt -e \"CREATE DATABASE IF NOT EXISTS qservScratch;\"");
	run_command("./bin/mysql -S var/lib/mysql/mysql.sock -u root $pass_opt -e \"GRANT ALL ON qservResult.* TO \'qsmaster\'\@\'localhost\';\"");
	run_command("./bin/mysql -S var/lib/mysql/mysql.sock -u root $pass_opt -e \"GRANT ALL ON qservScratch.* TO \'qsmaster\'\@\'localhost\';\"");
	run_command("./bin/mysql -S var/lib/mysql/mysql.sock -u root $pass_opt < qserv/master/examples/qserv-master-perms.sql");
        # TODO : only on workers
	run_command("./bin/mysql -S var/lib/mysql/mysql.sock -u root $pass_opt < tmp/qservw_workerid.sql");
	
	#shutdown mysql
	run_command("./bin/mysqladmin -S var/lib/mysql/mysql.sock shutdown -u root $pass_opt");
	
}

#check on extra source tar, compile, install, and configure for use.
sub check_and_setup_extras {

	print "Installing Protobuf from source.\n";
	
	print "chdir to $build_dir\n" if( $debug );
	
	chdir "$build_dir";
	my $protobuf_dir = check_and_untar( "protobuf" );

	chdir "$build_dir/$protobuf_dir";
	run_command("./configure --prefix=\"$install_dir\"");
	run_command("make");
	run_command("make install");
	
	chdir "$build_dir/$protobuf_dir/python";
	run_command("$install_dir/bin/python setup.py install");
	
	print "Installing MySQL py interface.\n";
	
	chdir "$build_dir";
	my $mysqldb_dir = check_and_untar( "MySQL-python" );
	
	chdir "$build_dir/$mysqldb_dir";
	run_command("echo -- DEBUG --");
	run_command("env");
	run_command("echo -- DEBUG --");
	run_command("python setup.py build");
	run_command("python setup.py install");

	print "Installing zope 3 interface.\n";
	
	chdir "$build_dir";
	my $zope3_dir = check_and_untar( "zope.interface" );
	
	chdir "$build_dir/$zope3_dir";
	run_command("python setup.py build");
	run_command("python setup.py install");

	print "Installing numpy interface.\n";

	chdir "$build_dir";
	my $numpy_dir = check_and_untar( "numpy" );
	if( $numpy_dir ) {
		chdir "$build_dir/$numpy_dir";
		run_command("python setup.py build");
		run_command("python setup.py install");
	}

	print "Installing Twisted interface.\n";
	
	chdir "$build_dir";
	my $twisted_dir = check_and_untar( "Twisted" );
	
	chdir "$build_dir/$twisted_dir";
	run_command("python setup.py install");
	
	chdir "$install_dir";

}

#check on mysql source tar, compile, install
sub check_and_install_mysql {

	print "Installing MySQL from source.\n";
	
	print "chdir to $build_dir\n" if( $debug );
	
	chdir "$build_dir";

        #install curses lib if it is in place
	my $ncurses_dir = check_and_untar( "ncurses" );
	if( $ncurses_dir ) {
		chdir "$build_dir/$ncurses_dir";
		run_command("./configure --prefix=\"$install_dir\"");
		run_command("make");
		run_command("make install");
	}

	$ENV{'LDFLAGS'} = "-L$install_dir/lib";

	my $mysql_dir = check_and_untar( "mysql-5" );

        # check if it is already installed
        if( -e "$install_dir/bin/mysqld_safe" ) {
                print "WARNING: MySQL already installed...\n";
                return;
        }
	chdir "$build_dir/$mysql_dir";
	run_command("./configure --prefix=\"$install_dir\" --enable-local-infile --with-mysqld-user=$prod_user");
	run_command("make");
	run_command("make install");
	
}

# configure mysql for use
sub init_mysql_db {

	system("killall mysqld mysqld_safe");	
        run_command("rm -rf $mysql_data_dir/*");
	
	run_command("mysql_install_db --defaults-file=$install_dir/etc/my.cnf --user=$prod_user");
	
	if ( length($db_pass) > 0) {
		print "Starting mysql server.\n";
		system("$install_dir/bin/mysqld_safe --defaults-file=$install_dir/etc/my.cnf &");
		sleep 5;
		my $quiet=1;	
		print ("Changing mysql root password.");
                # password for root@127.0.0.1 won't be updated with next command.
		# run_command("mysqladmin -S $install_dir/var/lib/mysql/mysql.sock -u root password '$db_pass'", $quiet);
		# run_command("mysqladmin -h $fqdn -P$mysql_port -u root password '$db_pass'", $quiet);
		
		run_command("$install_dir/bin/mysql -S $install_dir/var/lib/mysql/mysql.sock -u root < $install_dir/tmp/mysql-password.sql");
		run_command("rm $install_dir/tmp/mysql-password.sql");

		# shutdown mysql
		run_command("$install_dir/bin/mysqladmin -S $install_dir/var/lib/mysql/mysql.sock shutdown -u root -p'$db_pass'", $quiet);
	}
	else {
		print "Please set the mysql root user password with: ";
		print "mysqladmin -S $install_dir/var/lib/mysql/mysql.sock -u root password <password>\n";
		print "mysqladmin -u root -h $fqdn -P$mysql_port password <password>\n";
	}

	chdir "$install_dir";

}

#setup a local python with virtual env. for install of extra python libs
sub check_and_setup_python {

	chdir "$build_dir";
	my $virtualenv_dir = check_and_untar( "virtualenv" );
	
	chdir "$build_dir/$virtualenv_dir";
	run_command("$python_path virtualenv.py $install_dir");
	
	chdir "$install_dir";

}

#check on lua source tars, compile, install, and configure for use.
sub check_and_install_lua {

	print "Installing lua from source.\n";
	
	#install readline if it is there
	chdir "$build_dir";
	my $readline_dir = check_and_untar( "readline" );
        if( $readline_dir ) {
		chdir "$build_dir/$readline_dir";
       		run_command("./configure --prefix=\"$install_dir\"");
        	run_command("make");
        	run_command("make install");
	}
	
	print "Installing lua from source.\n";
	
	#install lua
	chdir "$build_dir";
	my $lua_dir = check_and_untar( "lua-5.1.4" );
	chdir "$build_dir/$lua_dir";
	run_command("perl -pi -e \"s/PLAT= none/PLAT= linux/\" Makefile");
	run_command("perl -pi -e \"s,INSTALL_TOP= /usr/local,INSTALL_TOP= $install_dir,\" Makefile");
	run_command("perl -pi -e \"s,CFLAGS= -O2,CFLAGS= -I$install_dir/include -fPIC -O2,\" src/Makefile");
	run_command("perl -pi -e \"s,LIBS= -lm,LIBS= -L$install_dir/lib -lm,\" src/Makefile");
	run_command("make");
	run_command("make install");
	
	#install luasocket
	chdir "$build_dir";
	my $luasocket_dir = check_and_untar( "luasocket" );
	chdir "$build_dir/$luasocket_dir";
	run_command("perl -pi -e \"s,INSTALL_TOP_SHARE=/usr/local/share/lua/5.1,LUAINC=-I$install_dir/include\nINSTALL_TOP_SHARE=$install_dir/share/lua/5.1,\" config");
	run_command("perl -pi -e \"s,INSTALL_TOP_LIB=/usr/local/lib/lua/5.1,INSTALL_TOP_LIB=$install_dir/lib/lua/5.1,\" config");
	run_command("make");
	run_command("make install");
	
	#install lua xmlrpc
	chdir "$build_dir";
	my $luaxmlrpc_dir = check_and_untar( "lua-xmlrpc" );
	chdir "$build_dir/$luaxmlrpc_dir";
	run_command("perl -pi -e \"s,LUA_DIR= /usr/local,LUA_DIR= $install_dir,\" Makefile");
	run_command("make");
	run_command("make install");
	
	#install expat lib
	chdir "$build_dir";
	my $expat_dir = check_and_untar( "expat" );
	chdir "$build_dir/$expat_dir";
	run_command("./configure --prefix=\"$install_dir\"");
	run_command("make");
	run_command("make install");
	
	#install luaexpat
	chdir "$build_dir";
	my $luaexpat_dir = check_and_untar( "luaexpat" );
	chdir "$build_dir/$luaexpat_dir";
	run_command("perl -pi -e \"s,/usr/local,$install_dir,\" config");
	run_command("perl -pi -e \"s,5\.0,5\.1,\" config");
	run_command("perl -pi -e \"s,-O2,-O2 -fPIC,\" config");
	run_command("perl -pi -e \"s,LUA_VERSION_NUM= 500,LUA_VERSION_NUM= 514,\" config");
	run_command("perl -pi -e \"s,EXPAT_INC=(.*),EXPAT_INC=\1\nEXPAT_LIBDIR= $install_dir/lib,\" config");
	run_command("perl -pi -e 's,-lexpat,-L\\\$\\(EXPAT_LIBDIR\\) -lexpat,' makefile");
	run_command("make");
	run_command("make install");

}

#check on source tar for mysql proxy, compile, install, and configure for use
sub check_and_install_myproxy {

	print "Installing mysql-proxy from source.\n";

	#need to install lib event first.
	chdir "$build_dir";
	my $libevent_dir = check_and_untar( "libevent" );
	chdir "$build_dir/$libevent_dir";
	run_command("./configure --prefix=\"$install_dir\"");
	run_command("make");
	run_command("make install");
	
	#install mysql proxy
	chdir "$build_dir";
	my $myproxy_dir = check_and_untar( "mysql-proxy" );
	chdir "$build_dir/$myproxy_dir";
	$ENV{'LDFLAGS'} = "-L$install_dir/lib";
	$ENV{'LD_LIBRARY_PATH'} = "$install_dir/lib:$ENV{'LD_LIBRARY_PATH'}";
	$ENV{'CFLAGS'} = "-I$install_dir/include";
	$ENV{'LIBS'} = "-L$install_dir/lib -levent -llua -lm";
	$ENV{'LUA_LIBS'} = "-llua";
	$ENV{'LUA_CFLAGS'} = "-I$install_dir/include -L$install_dir/lib";
	run_command("./configure --prefix=\"$install_dir\" --with-lua");
	run_command("make");
	run_command("make install");

}

#A routine to check on the existence of a tar or zip file, and untar the
#file and return the created sub dir.  Based on substring in tar file name.
sub check_and_untar {
	my( $check_string ) = @_;
	
	print "Need to check and untar next package from build-dir : $check_string.\n" if( $debug );
	
	#check on version of virtual env
	my @tmplist = sort grep /$check_string/, @buildfiles;
	
	print "list of $check_string files : @tmplist.\n" if( $debug );
	
	my $tmp_dir;
	if( $tmplist[0] =~ /tar.gz/ ) {
		#need to first untar the 
		print "Untarring $tmplist[0].\n";
		chdir "$build_dir";
		run_command("tar zxf $tmplist[0]");
		( $tmp_dir ) = $tmplist[0] =~ /^(\S+).tar.gz/;
	}  elsif( $tmplist[0] =~ /tar.bz/  ) {
		#need to first untar the 
		print "Untarring $tmplist[0].\n";
		chdir "$build_dir";
		run_command("tar jxf $tmplist[0]");
		( $tmp_dir ) = $tmplist[0] =~ /^(\S+).tar.bz/;
	} elsif( $tmplist[0] =~ /zip/  ) {
		#need to first untar the 
		print "Unzipping $tmplist[0].\n";
		chdir "$build_dir";
		run_command("unzip $tmplist[0]");
		#need to refresh, unzip dir might have a different name than zip file.
		list_build_dir();
		my @tmplist2 = sort grep /$check_string/, @buildfiles;
		print "Using $tmplist2[0].\n";
		( $tmp_dir ) = $tmplist2[0];
	} else {
		print "Using $tmplist[0].\n";
		$tmp_dir = $tmplist[0];
	}
	
	return $tmp_dir;
}

sub create_symlink {

	my $target = $_[0];
	my $link = $_[1];
	-e $link and rmtree($link);
	symlink ($target,$link) or die print "Error creating symlink $link : $!\n";

}

sub check_and_deploy_qserv {

	chdir "$install_dir";

	if (length($src_dir) > 0) {
		if (-e "$src_dir")  {
			create_symlink ($src_dir,"$install_dir/qserv");
		}
		else {
			die print "Invalid path to Qserv source dir : $src_dir";
		}
	}

	#check if things are already untarred
	unless( -e "$install_dir/qserv" ) { 

		#check on version of qserv, and get last version
		my @tmplist = sort grep /qserv/, @buildfiles;
		my $qserv_tar = '';
		foreach my $file ( sort @tmplist ) {
			if( $file =~ /tar.gz/ ) {
				$qserv_tar = $file;
			}
		}

		#untar the qserv code
		if( -e "$build_dir/$qserv_tar" ) {
			if( $opts{'test'} ) {
				print "I would untar $qserv_tar here.\n";
			} else {
				print "Untarring qserv...\n";
				run_command("tar zxf $build_dir/$qserv_tar");
				my( $tmp_qserv ) = $qserv_tar =~ /^(\S+).tar.gz/;
				rename "$tmp_qserv", "qserv";
			}
		} else {
			print "Sorry - I can't find the qserv####.tar.gz in the build dir.\n";
			exit(0);
		}
	}
}

#remove all sub dir created, and revert back to before install with only
#source tar files in the build dir.  Will remove all loaded data also.
sub clean_all {
    
        run_command("rm -rf $log_dir || rm -rf $log_dir/*");
        run_command("rm -rf $mysql_data_dir || rm -rf $mysql_data_dir/*");
        run_command("rm -rf $install_dir/qserv/master/dist");

	chdir "$install_dir";
		
	print "Cleaning dir : $install_dir\n" if ($debug); 
	opendir TMPDIR, "$install_dir";
	my @listfiles = readdir TMPDIR;
	closedir TMPDIR;

	foreach my $file ( @listfiles ) {
		next if( $file =~ /^\./ );
		next if( $file =~ /build/ );
		if( -d $file ) {
			print "Removing $file\n";
			run_command("rm -r $file");
		}
	}
	
	clean_dir($build_dir);
	
}

# Remove file and directories, except *.tar.gz
sub clean_dir {

	my($dir) = @_;

	print "Cleaning dir : $dir\n" if ($debug); 
	chdir "$dir";

	opendir TMPDIR, "$dir";
	my @listfiles = readdir TMPDIR;
	closedir TMPDIR;

	foreach my $file ( @listfiles ) {
		next if( $file =~ /^\./ );
		next if( $file =~ /^.*\.tar\.gz$/ );
		next if( $file =~ /^.*\.tar\.bz2$/ );
		next if( $file =~ /^.*\.tar\.zip$/ );
		print " file : $file\n" if ($debug);
		if( -d $file ) {
			print "Removing directory : $file\n";
			run_command("rm -rf $file");
		}
		elsif( -e $file ) {
			print "Removing $file\n";
			run_command("rm $file");
		}
	}
}

#help report for the --help option
sub usage {
  my($exit, $message) = @_;

        my($bin) = ($0 =~ m!([^/]+)$!);
        print STDERR $message if defined $message;
        print STDERR <<INLINE_LITERAL_TEXT;     
NAME: 
	$bin [options]

DESCRIPTION:
	Take the required set of tar files, and confirgure and build a working
	qserv install.
	
EXAMPLES:
	$bin 

OPTIONS:
      	--debug			Print out debug messages.
     -h,--help			Print out this help message.
      	--test			Test install proceedure.
     -d,--install-dir		Install dir, default is use the current working dir.
     -l,--log-dir       	Log dir, default is use the install dir suffixed with var/log
      	--build-dir  		Path the source tar files, default to use "build"
	--clean-all 		Remove all produced software and data, revert to start before install
	--extras    		Install only the extra modules needed for qserv
	--mysql-port     	Specify mysql port (default to 3306)
	--mysql-pass 	        Specify mysql db root passwordd
     -m,--mysql-data-dir	Mysql data dir, default the install dir suffixed with var/lib/mysql
	--python    		Path the python install for use, default is /usr/bin/python
	--geometry-dir		Specify path to geometry source directory (if not in $src_dir/master/python/)
	--user      		Username to use in qserv use, default it to use current username.
	--test      		Test the install, don't perform any commands.
	
One of these option may also be setted :
	--lua       		Install only lua and needed lua modules
	--mysql     		Install only the mysql server, Erase and reinstall mysql data
	--mysql-proxy 		Install only the mysql proxy software.
	--init-mysql-db     	Erase and reinstall mysql data
	--xrootd    		Install only xrootd
	--qserv     		Install only qserv

Examples: $bin

Comments to Douglas Smith <douglas\@slac.stanford.edu>.
INLINE_LITERAL_TEXT

	       exit($exit) if defined $exit;

}

sub remove_trailing_slash {
	my( $string ) = @_;
	$string =~ s/\/$//;
	return $string;
}

sub run_command {
	my( $command, $quiet ) = @_;
	my $cwd = cwd();
	my @return;
	print "-- Running: $command in $cwd\n" unless( $quiet );
	open( OUT, "$command 2>&1 |") || die "ERROR : can't fork $command : $!";
	while( <OUT> ) {       
		print STDOUT $_; 
		push( @return, $_ ); 
	}
	close( OUT ) || die "ERROR : $command exits with error code ($?)";
	return @return;
}

