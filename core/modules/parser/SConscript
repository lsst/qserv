# -*- python -*-
# SConscript for parser module
Import('env')
Import('standardModule')

import os

standardModule(env)

# Import SqlSQL2ImpTokenTypes.txt from query/
tokenFile = env.Command("SqlSQL2ImpTokenTypes.txt",
                        "../query/SqlSQL2Tokens.txt",
                        Copy("$TARGET", "$SOURCE"))

antlrTgts = env.Antlr(['SqlSQL2.g'],
                      ANTLR_GLIB='DmlSQL2.g',
                      ANTLR_OUT='.',
                      )
                  
antlrSrcs = [f for f in antlrTgts if str(f).endswith(env.subst('$ANTLR_CCSUFFIX'))]
antlrHdrs = [f for f in antlrTgts if str(f).endswith(env.subst('$ANTLR_HSUFFIX'))]

env.Depends(antlrSrcs[0], 'DmlSQL2.g')
env.Depends(antlrSrcs[0], tokenFile)

build_data = DefaultEnvironment()['build_data']
build_data['module_objects']['parser'] += antlrSrcs
