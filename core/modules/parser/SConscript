# -*- python -*-
## SConscript for parser module
Import('env')
Import('extraTgts')
import os

myDeps = filter(lambda i: not os.path.basename(str(i)).startswith("test"), env.Glob("*.cc"))

# Import SqlSQL2ImpTokenTypes.txt from query/
tokenFile = env.Command("SqlSQL2ImpTokenTypes.txt",
                        "../query/SqlSQL2Tokens.txt",
                        Copy("$TARGET", "$SOURCE"))

glib=File('DmlSQL2.g')

antlrTgts = env.Antlr(['SqlSQL2.g'],
    ANTLR_GLIB=env.File('DmlSQL2.g'),
    ANTLR_OUT='.',
    )

antlrSrcs = filter(lambda f: str(f).endswith(env.subst('$ANTLR_CCSUFFIX')), antlrTgts)
antlrHdrs = filter(lambda f: str(f).endswith(env.subst('$ANTLR_HSUFFIX')), antlrTgts)

env.Depends(antlrSrcs[0], 'DmlSQL2.g')
env.Depends(antlrSrcs[0], tokenFile)
extraTgts['antlrHdrs'] = antlrHdrs
#print "ANTLR srcs=", map(str, antlrSrcs)
#print "ANTLR hdrs=", antlrHdrs
# Ignore output antlr .txt files for now.
outs = antlrSrcs + myDeps
Return('outs')
